#define TRACK_END_FRAME (1920)
#define TRACK_PANEL_HANDLERS (
   (enter
      {foreach $obj {draw_order.grp get_array objects}
         {if {$obj is_a BandLabel}
            {draw_order.grp remove_object $obj}
         }
      }
      {game add_sink $this}
      {with {$this loaded_dir}
         {do
            ($hwss
               {if_else {== $hwlength 1.0}
                  1.0
                  {if_else {> $hwlength 2.0}
                     {/ $hwlength 1.4}
                     {if_else {> $hwlength 1.0}
                        {/ $hwlength {+ {* 0.4 $hwlength} 0.6}}
                        {* $hwlength {+ {* -1.5 $hwlength} 2.5}}
                     }
                  }
               }
            )
            {set [view_time_easy] 
               {* {* {/ {if_else $syncdifspeeds 1.2 2.4} $hwss} $trackspeed} $speedmod}
            }
            {set [view_time_expert] 
               {* {* {/ 1.2 $trackspeed} $hwss} $speedmod}
            }
         }
      }
      {$this set_showing
         {if_else
            {'||'
               $skip_intro
               {gamemode get is_drum_trainer}
               {gamemode get is_practice}
            }
            {gamemode get show_track}
            FALSE
         }
      }
   )
   (exit {game remove_sink $this})
   (unison_hit)
   (unison_miss)
   (set_track_out)
   (set_track_in)
   (animate_track ($start $end $period $units))
   (animate_track_out)
   (animate_track_out_fast)
   (animate_track_in_fast)
   (finish_load
      {if {!= $gems lrb} {do
         ($gems_dir {new RndDir gems_dir})
         {switch $gems
            #include ../ulti/track/gems/gems_scripting.dta
         }
      }}
      {if {!= $smashers lrb}
         {smasher_ps2.tex set_bitmap {sprintf "ulti/track/smashers/%s.png" $smashers}}
         {smasher_color_ps2.tex set_bitmap {sprintf "ulti/track/smashers/%s_color.png" $smashers}}
         {square_smasher_bright.tex set_bitmap {sprintf "ulti/track/smashers/%s_square.png" $smashers}}
      }
      ; apply custom colors to lanes. this is hacky as FUCK however it actually works perfectly. i'm really proud of this.
      ; however like i'm really tired so i might be dumb and am overlooking a sane way of doing this.
      {foreach $gtr (guitar bass)
         {with {{get_track_panel} find $gtr}
            {foreach_int $i 0 5
               {{new PropAnim {sprintf "gem_bak%d.anim" $i}} copy {sprintf "gem_mash%d.anim" $i} 0}
            }
            {foreach_int $i 0 5 {do
               ($wid {sprintf "gem_mash%d.wid" $i})
               ($color {eval_var {sprint gtrsm {+ $i 1}}})
               {clear_array $wid meshes}
               {$wid insert (meshes 0) {sprintf "gem_mash%d.mesh" $color}}
               {$wid insert (meshes 0) {sprintf "gem_mash%d.1.mesh" $color}}
               {{sprintf "gem_mash%d.anim" $i} copy {sprintf "gem_bak%d.anim" $color} 0}
            }}
         }
      }
      {with {{get_track_panel} find drum}
         {unless $kickbounce
            {clear_array kick_drummer.trig anims}
         }
         {foreach_int $i 1 5
            {{new PropAnim {sprintf "drum_bak%d.anim" $i}} copy {sprintf "drum_mash%d.anim" $i} 0}
            {{new PropAnim {sprintf "drum_bak%d_BRE.anim" $i}} copy {sprintf "drum_mash%d_BRE.anim" $i} 0}
         }
         {foreach_int $i 1 5 {do
            ($wid {sprintf "drum_mash%d.wid" $i})
            ($color
               {switch {eval_var {sprint drms $i}}
                  (0 4)
                  {eval_var {sprint drms $i}}
               }
            )
            {clear_array $wid meshes}
            {$wid insert (meshes 0) {sprintf "drum_mash%d.mesh" $color}}
            {$wid insert (meshes 0) {sprintf "drum_mash%d.1.mesh" $color}}
            {{sprintf "drum_mash%d.anim" $i} copy {sprintf "drum_bak%d.anim" $color} 0}
            {{sprintf "drum_mash%d_BRE.anim" $i} copy {sprintf "drum_bak%d_BRE.anim" $color} 0}
         }}
      }
      {$this set_showing FALSE}
      {$this set_track_out}
   )
   (intro_start
      {$this reset}
      {$this set_showing {gamemode get show_track}}
      {$this set_track_in}
   )
   (intro_skip
      {$this reset}
      {$this set_showing {gamemode get show_track}}
      {$this set_track_out}
      {$this play_intro}
   )
   (on_reset
      {beatmatch foreach_active_player $m
         {$m on_new_track}
         {{$m track} init {$m track}}
      }
      {{$this loaded_dir} reset}
   )
   (on_extend
      {force_drum_freestyle FALSE}
      {beatmatch foreach_active_player $player ;handle adding the appropriate callbacks to each player
         {do
            ($obj {sprint {$player instrument} "_track_callback"})
            {if {exists $obj} {delete $obj}}
            {new Object $obj}
            {$obj set_type track_callback}
            {$obj init $player}
         }
         {unless {|| {! $countdown} {gamemode get is_practice} {gamemode get is_drum_trainer} {challenge_mgr has_challenge}}
            {do
               ($obj {sprint {$player instrument} "_countdown"})
               ($dir
                  {sprint "endgame_feedback_"
                     {if_else {== {$player instrument} vocals}
                        vox
                        {do
                           ($str {sprint {{$player instrument} get player_feedback}})
                           {sprint 0 {str_elem $str {- {strlen $str} 1}}}
                        }
                     }
                  }
               )
               {if {exists $obj} {delete $obj}}
               {new Object $obj}
               {$obj set_type countdown}
               {$obj init
                  {$player instrument}
                  {$dir find end_game_drum.lbl}
                  {$dir find end_game_drum.tnm}
               }
            }
         }
      }
      {$this play_intro}
      {if {&& {exists gamemode} {gamemode get play_track_intro_sfx}}
         {synth play track_beg.cue}
      }
      {if_else {== {gamemode in_mode} practice}
         {do
            {unless {> {gamecfg get practice_speed} 0}
               {beatmatch set_music_speed $speedmod}
            }
            {if {> {gamecfg get practice_speed} 0}
               {beatmatch set_music_speed $modifier}
            }
         }
         {beatmatch set_music_speed $speedmod}
      }
   )
)
{new TrackPanel
   coop_track_panel
   (file ../track/trackpanel.milo)
   TRACK_PANEL_HANDLERS
}
{new TrackPanel
   h2h_track_panel
   (file
      {if_else {== {{game get_participant_config 0} get_track_sym} vocals}
         ../track/trackpanel_h2h_vocals.milo
         ../track/trackpanel_h2h.milo
      }
   )
   TRACK_PANEL_HANDLERS
}
{func get_track_panel
   {gamemode get track_panel}
}
{set $playback_file ""}