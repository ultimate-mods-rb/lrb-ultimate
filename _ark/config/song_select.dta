#define SONG_SELECT_ALPHA_SYMBOLS (store_nonalpha_group A B C D E F G H I J K L M N O P Q R S T U V W X Y Z)
(song_select_gap 30.0)
(sorts
   (by_difficulty
      (cmp_song_to_song
         ($lhs $rhs)
         {$this cmp $lhs $rhs (kSTRank $cmp.rank_part) (kSTArtist) (kSTName)}
      )
      (cmp_init {set $cmp.rank_part {song_offer_provider sort_difficulty_part}})
      (browser_group
         ($offer)
         {do
            ($part {song_offer_provider sort_difficulty_part})
            ($tier {song_mgr rank_tier {$offer get_data (rank $part)} $part})
            {song_mgr rank_tier_token $tier}
         }
      )
   )
   (by_song
      (cmp_song_to_song
         ($lhs $rhs)
         {$this cmp $lhs $rhs (kSTName)}
      )
      (browser_group
         ($offer)
         {$offer first_char name FALSE}
      )
      (shortcut_groups SONG_SELECT_ALPHA_SYMBOLS)
   )
   (by_artist
      (cmp_song_to_song
         ($lhs $rhs)
         {$this cmp $lhs $rhs (kSTArtist) (kSTName)}
      )
      (browser_group
         ($offer)
         {$offer artist}
      )
      (browser_subgroup
         ($offer)
         {if_else {$offer has_data album_name}
            {$offer album_name}
            'gNullStr;'
         }
      )
      (browser_shortcut
         ($offer)
         {$offer first_char artist TRUE}
      )
      (browser_cover
         ($offer)
         {$offer is_cover}
      )
      (shortcut_groups SONG_SELECT_ALPHA_SYMBOLS)
   )
   (by_genre
      (cmp_song_to_song
         ($lhs $rhs)
         {$this cmp $lhs $rhs (kSTGenre) (kSTArtist) (kSTName)}
      )
      (browser_group
         ($offer)
         {$offer genre}
      )
   )
   (by_year
      (cmp_song_to_song
         ($lhs $rhs)
         {if_else {$lhs has_data year_released}
            {if_else {$rhs has_data year_released}
               {$this cmp $lhs $rhs (kSTYearReleased TRUE) (kSTArtist) (kSTName)}
               -1
            }
            {if_else {$rhs has_data year_released}
               1
               {$this cmp $lhs $rhs (kSTArtist) (kSTName)}
            }
         }
      )
      (browser_group
         ($offer)
         {sprint
            {if_else {$offer has_data year_released}
               {$offer year_released}
               invalid
            }
         }
      )
      (sub by_artist)
   )
   (by_origin
      (cmp_song_to_song
         ($lhs $rhs)
         {do
            ($origin_cmp
               {badcmp
                  {if_else
                     {&&
                        {$lhs has_data game_origin}
                        {!= {$lhs get_data (game_origin)} rb2}
                     }
                     {$lhs get_data (game_origin)}
                     {$lhs get_icon}
                  }
                  {if_else
                     {&&
                        {$rhs has_data game_origin}
                        {!= {$rhs get_data (game_origin)} rb2}
                     }
                     {$rhs get_data (game_origin)}
                     {$rhs get_icon}
                  }
               }
            )
            {if_else $origin_cmp
               $origin_cmp
               {$this cmp $lhs $rhs (kSTName)}
            }
         }
      )
      (browser_group
         ($offer)
         {sprint
            {if_else
               {&&
                  {$offer has_data game_origin}
                  {!= {$offer get_data (game_origin)} rb2}
               }
               {$offer get_data (game_origin)}
               {$offer get_icon}
            }
         }
      )
   )
)