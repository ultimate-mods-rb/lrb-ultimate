{new
   UIPanel
   bonus_menupanel
   (file
      "quickplay/quickplay_venue.milo")
   (selection_memory 0)
   (load
      {content_mgr start_refresh}
      {content_loading_panel allowed_to_show TRUE})
   (finish_load
      {content_loading_panel allowed_to_show FALSE})
   (is_loaded
      {content_mgr refresh_done})
   (enter
      {do
         {choosevenue.lbl set text_token "bonus"}
         {delete Venue_BG_BG.mesh}
         {delete Venue_BG_Border.mesh}
         {venue.lst
            set_data
            (
               theatre
               framerate
               #ifdef HX_NG
               musicstore
               #endif
            )
         }
         {$this set_focus venue.lst}
         {venue.lst
            set_selected
            [selection_memory]}})
   (BUTTON_DOWN_MSG
      {if
         {== $action kAction_Cancel}
         {synth play button_back}}
      kDataUnhandled)
   (SELECT_MSG
      {do
         ($selected
            {venue.lst selected_sym})
         {$this exec_sel $selected}})
   (SCROLL_MSG
      {synth play button_focus}
      {$this
         set
         selection_memory
         {venue.lst selected_pos}})
   (exec_sel
      ($bonus_sel)
      {synth play button_select}
      {switch
         $bonus_sel
         (theatre
            {do
               {if_else
                  {gamecfg is_missing_mic $player_num FALSE}
                  {do
                     #ifdef KIOSK_DEMO
                     {vocalist_must_have_mic_screen set message demo_solo_vocalist_needs_mic}
                     #else
                     {vocalist_must_have_mic_screen set message solo_vocalist_needs_mic}
                     #endif
                     {ui push_screen vocalist_must_have_mic_screen}}
                  {do
                     {gamemode set_mode theatre $player_num}
                     {content_mgr start_refresh}
                     #ifdef KIOSK_DEMO
                     {challenge_mgr clear_current_challenge}
                     #endif
                     {ui
                        goto_screen
                        {gamemode get matchmaking_screen}}}}}
         )
         (framerate
            {ui goto_screen frame_rate_screen}
         )
         (musicstore
            {do
               #ifdef HX_XBOX
               {input_mgr
                  set_user
                  {user_mgr get_band_user $player_num}}
               {gamemode set_mode store}
               {if_else
                  {'||'
                     {!
                        {platform_mgr is_player_signed_into_live $player_num}}
                     {platform_mgr is_player_a_guest $player_num}}
                  {ui push_screen main_store_signin_screen}
                  {ui push_screen main_store_find_cache_screen}}
               #endif
               #ifdef HX_PS3
               {gamemode set_mode store}
               {ui goto_screen store_enter_network_screen}
               #endif
               #ifdef HX_WII
               {ui goto_screen store_loading_screen}
               {force_goto_else_after_network_connect store_wii_enter_screen main_screen TRUE}
               #endif
               #ifdef HX_PC
               {ui goto_screen store_loading_screen}
               #endif
               {fx fade_out}
               {input_mgr
                  set_user
                  {user_mgr get_band_user $player_num}}}
         )
      }
   )}
{new
   MainScreen
   bonus_menuscreen
   (back main_screen)
   (panels meta movie_panel bonus_menupanel tour_lower3rd_panel)
   (helpbar
      ((cancel helpbar_back)
         (confirm helpbar_confirm)))
   (enter
      {$this set_focus_panel bonus_menupanel})}